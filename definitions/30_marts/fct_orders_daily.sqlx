config {
  type: "incremental",
  schema: "analytics_dev",
  name: "fct_orders_daily",
  tags: ["prod"],
  uniqueKey: ["order_date", "customer_id"],
  bigquery: {
    partitionBy: "order_date",
    requirePartitionFilter: true
  }
}

with daily as (
  select
    order_date,
    customer_id,
    count(distinct order_id) as nb_orders,
    sum(amount) as total_amount
  from ${ref("int_orders_enriched")}

  ${when(incremental(),
    `where order_date >= date_sub(current_date(), interval 7 day)`,
    ``
  )}

  group by 1,2
)

select
  order_date,
  customer_id,
  nb_orders,
  total_amount
from daily